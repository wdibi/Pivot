Pivot {
  Program             = Block
  Block               = Statement+
  Statement           = FunctionDeclaration
                      | VarDeclaration
                      | IfStatement
                      | IterationStatement
                      | CallStatement
                      | PrintStatment
                      | Assignment
  VarDeclaration      = "all" Type  NonemptyListOf<id, ","> "<-" NonemptyListOf<Exp, ","> ";"   -- multi
                      | Type id "<-" (CallStatement | IfShort | Exp) ";"                        -- single
                      |  "(" Type id ("," Type id)+ ")"
                        "<-" "(" NonemptyListOf<Exp, ","> ")" ";"                               -- multidec
  Assignment          = id "<-" (CallStatement | Exp) ";"
  IfStatement         = "if" Exp "then" Statement+ ("else" Statement+)? ends
  IfShort             = Exp "when" (CallStatement | Exp )
                        "otherwise" (CallStatement | IfShort | Exp )
  ReturnStatement     = "return" (Exp | IfShort) ";"
  IterationStatement  = "while" Exp "do" Block ends                                             -- while
                      | "repeat" Block "until" Exp ";"                                          -- loop
                      | "for" VarDeclaration (Exp | CallStatement)
                      	";" (CallStatement | Assignment) "do" Block ends                        -- for
  PrintStatment       = print (CallStatement | Exp)  ";"
  FunctionDeclaration = id "(" Params ")" "->" Type Block? ReturnStatement ends                 -- func
                      | "task" id "(" Params ")" Block ends                                     -- task
  Params              = (Type id ("," Type id)*)?
  CallStatement       = id "(" listOf<Exp, ","> ")"                                             -- function
                      | ((id | List | Dict) ("<<" id "(" listOf<Exp, ","> ")")+)                -- method
  Exp                 = Exp orop Exp1                                                           -- or
                      | Exp andop Exp1                                                          -- and
                      | Exp1
  Exp1                = Exp2 relop Exp2                                                         -- binary
                      | Exp2
  Exp2                = Exp2 addop Exp3                                                         -- binary
                      | Exp3
  Exp3                = Exp3 mulop Exp4                                                         -- binary
                      | Exp4
  Exp4                = prefixop Exp4                                                           -- unary
                      | Exp5
  Exp5                = Exp5 "**" Exp6                                                          -- binary
                      | Exp6
  Exp6                = id
                      | boollit
                      | numlit
                      | strlit
                      | charlit
                      | List                                                                    -- list
                      | Dict                                                                    -- dict
                      | "(" Exp ")"                                                             -- parens
  List                = "[" ListOf<Exp, ","> "]"
  Dict                = "{" ListOf<KeyValuePair, ","> "}"
  Type                = ( NumType | StringType | CharType | BooleanType | ListType | DictType | AutoType )
  NumType             = "num"
  StringType          = "str"
  CharType            = "char"
  BooleanType         = "bool"
  ListType            = "[" Type "]"
  DictType            = "{" Type ":" Type "}"
  AutoType            = "_"
  KeyValuePair        = Exp ":" Exp
  keyword             = ("all" | "while" | "repeat" | "if" | "else"
                      | "return" | "then" | "and" | "or" | "when" | "otherwise") ~idrest
  id                  = ~keyword ("_" | letter | "$") idrest*
  idrest              = "_" | alnum
  numlit              = digit+ ("." digit+)?
  boollit             = "true" | "false"
  char                = escape
                      | ~"\\" ~"\"" ~"'" ~"\n" any
  charlit             = "'" (char | "\"") "'"
  strlit              = "\"" (char | "\'")* "\""
  escape              = "\\\\" | "\\\"" | "\\'" | "\\n"
  andop               = "and" | "AND" | "&&"
  orop                = "or" | "OR" | "||"
  addop               = "+" | "-"
  relop               = "<=" | "<" | "==" | "!=" | ">=" | ">"
  mulop               = "*" | "/" | "%"
  prefixop            = "-" | "not" | "!"
  newline             = "\n"+
  ends                = "\n"* "end"
  print               = "print" ~alnum
}